class FallingObject {
   // Posición actual del objeto
   field int x, y;
   // Tipo de objeto (2=moneda)
   field int type;
   // Velocidad de caída
   field int speed;
   // Tamaño del objeto
   field int size;
   // Si el objeto está activo en pantalla
   field boolean active;

   // Constructor: crea un nuevo objeto cayendo
   constructor FallingObject new(int ax, int atype, int aspeed) {
      let x = ax;
      let y = 10;  // Comienza en la parte superior
      let type = atype;
      let speed = aspeed;
      let size = 16;  // Tamaño por defecto
      let active = true;
      do draw();
      return this;
   }

   // Libera la memoria del objeto.
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }
   
   // Getter para obtener el tipo del objeto
   method int type() {
      return type;
   }

   // Dibuja el objeto según su tipo
   method void draw() {
      if (~active) {
         return;
      }

      if (type = 2) {
         do drawCoin();
      }
      return;
   }

// Dibuja una moneda (círculo de 16x16 píxeles)
method void drawCoin() {
   var int memAddress, location;

   // Calcular dirección de memoria en pantalla
   let location = (y * 32) + (x / 16);
   let memAddress = 16384 + location;

   // Dibujar círculo 16x16 usando Memory.poke
   do Memory.poke(memAddress + 32,  480);
   do Memory.poke(memAddress + 64, 2016);
   do Memory.poke(memAddress + 96, 4032);
   do Memory.poke(memAddress +128, 4032);
   do Memory.poke(memAddress +160, 4032);
   do Memory.poke(memAddress +192, 4032);
   do Memory.poke(memAddress +224, 2016);
   do Memory.poke(memAddress +256,  480);

   return;
}

   // Borra el objeto de la pantalla
   method void erase() {
      if (~active) {
         return;
      }

      if (type = 2) {
         do eraseCoin();
      }
      return;
   }


   // Borra la moneda escribiendo ceros en las posiciones de memoria
   method void eraseCoin() {
      var int location, memAddress;

      let location = (y * 32) + (x / 16);
      let memAddress = 16384 + location;

      do Memory.poke(memAddress + 32,  0);
      do Memory.poke(memAddress + 64,  0);
      do Memory.poke(memAddress + 96,  0);
      do Memory.poke(memAddress +128,  0);
      do Memory.poke(memAddress +160,  0);
      do Memory.poke(memAddress +192,  0);
      do Memory.poke(memAddress +224,  0);
      do Memory.poke(memAddress +256,  0);

      return;
   }



   // Actualiza la posición del objeto (lo hace caer)
   method void fall() {
      if (~active) {
         return;
      }
      
      do erase();
      let y = y + speed;
      
      // Si el objeto sale de la pantalla por abajo, reaparecerlo arriba
      if (y > 255) {
         let y = 10;  // Vuelve a aparecer arriba
         // Cambiar posición X de forma pseudo-aleatoria para variedad
         let x = (x + 73);
         // Asegurar que X esté en rango válido (0-480 para tener espacio para el objeto)
         while (x > 480) {
            let x = x - 480;
         }
         while (x < 0) {
            let x = x + 480;
         }
      }
      
      do draw();
      return;
   }

   // Verifica si hay colisión con el pájaro
   method boolean checkCollision(int birdX, int birdY, int birdSize) {
      if (~active) {
         return false;
      }
      
      // Detección de colisión simple (rectángulos que se sobreponen)
      if ((birdX < (x + size)) & ((birdX + birdSize) > x) &
          (birdY < (y + size)) & ((birdY + birdSize) > y)) {
         return true;
      }
      
      return false;
   }

   // Getter para verificar si el objeto está activo
   method boolean isActive() {
      return active;
   }

   // Desactiva el objeto (cuando es recogido por el pájaro)
   method void deactivate() {
      do erase();
      let active = false;
      return;
   }
   
   // Reactiva el objeto en una nueva posición
   method void reactivate() {
      // Posicionar arriba con nueva posición X pseudo-aleatoria
      let y = 0;
      let x = (x + 113);
      // Asegurar que X esté en rango válido
      while (x > 480) {
         let x = x - 480;
      }
      while (x < 0) {
         let x = x + 480;
      }
      let active = true;
      return;
   }
}
